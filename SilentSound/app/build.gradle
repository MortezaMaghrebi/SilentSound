plugins {
    id 'com.android.application'
}

android {
    namespace 'ir.zemestoon.silentsound'
    compileSdk 33

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        applicationId "ir.zemestoon.silentsound"
        minSdk 24
        targetSdk 33
        versionCode 3
        versionName "1.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "store"
    productFlavors {
        myket {
            dimension "store"
            //applicationId "ir.zemestoon.silentsound.myket"  // پکیج متفاوت
            versionNameSuffix "-myket"

            // تنظیمات مایکت
            def marketApplicationId = "ir.mservices.market"
            def marketBindAddress = "ir.mservices.market.InAppBillingService.BIND"
            manifestPlaceholders = [
                    marketApplicationId: "${marketApplicationId}",
                    marketBindAddress: "${marketBindAddress}",
                    marketPermission: "${marketApplicationId}.BILLING"
            ]
            buildConfigField "String", "IAB_PUBLIC_KEY", "\"MIGfMA0GCSqGSIb3DQEBAQUAA4GNADCBiQKBgQCutqcsnvRDoSDY0yYO+xGoNx/Ikcl04L9ka19lGpAdE4CYrrphs31VXqI9nC9AV7fkM/AkiHWe7iEjNAIpln/g2QJPqBCfK+x2/DwkViFo1XfRVe7kFjO0yAyBXMdvEjKOrj+mQKZcRmDiYq5Wa+ntObfyuHl4sxeGjZBQ/gnrkQIDAQAB\""
            buildConfigField "String", "STORE_NAME", "\"myket\""
        }

        bazaar {
            dimension "store"
           // applicationId "ir.zemestoon.silentsound.bazaar"  // پکیج متفاوت
            versionNameSuffix "-bazaar"

            // تنظیمات بازار
            def marketApplicationId = "com.farsitel.bazaar"
            def marketBindAddress = "ir.cafebazaar.pardakht.InAppBillingService.BIND"
            manifestPlaceholders = [
                    marketApplicationId: "${marketApplicationId}",
                    marketBindAddress: "${marketBindAddress}",
                    marketPermission: "com.farsitel.bazaar.permission.PAY_THROUGH_BAZAAR"
            ]
            buildConfigField "String", "IAB_PUBLIC_KEY", "\"MIHNMA0GCSqGSIb3DQEBAQUAA4G7ADCBtwKBrwCnonCeXAr6fC6Y1QDQOMo140bLrmOFLIcZp3I0M8uNc7IIfA6DecM1z4Y2OQdOYemPguqA6vyo4+3ysOhAui0C7D7y1ug35NK+G31/IlFY15RcaYhfcU8BSFIB5y5pyWGw32E0eaxYiMJBZNZPGfAFRmkkRK0B6PKapKuDZjMqP/DvJ93UZDttR4FWaXEzj7XXFiIrS3mKk+5R6RF9A+cp4nYi9HxX6e9RPXurK5MCAwEAAQ==\""
            buildConfigField "String", "STORE_NAME", "\"bazaar\""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_DEBUG_LOGS", "false"
        }
        debug {
            buildConfigField "boolean", "ENABLE_DEBUG_LOGS", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    implementation("ir.tapsell.plus:tapsell-plus-sdk-android:2.3.2")

    // وابستگی‌های مشترک
}

// وابستگی‌های خاص هر flavor
dependencies {
    myketImplementation 'com.github.myketstore:myket-billing-client:1.6'
    bazaarImplementation "com.github.cafebazaar.Poolakey:poolakey:2.2.0"
}